# This is a Snakemake file.


import yaml

# load populations definitions
with open('docs/_static/data/populations.yml', mode='r') as f:
    populations = yaml.load(f)
    pop_ids = list(populations.keys())

# analyse whole chromosomes
chromosomes = ['2', '3', 'X']

# parameter settings for the H12 build
h12_config = {
    'amplitude': 0.5,
    'min_amplitude': 0,
    'decay': 0.5,
    'min_decay': 0.15,
    'max_decay': {
        'AOM': 3,
        'BFM': 2,
        'GWA': 2,
        'GNS': 2,
        'BFS': 2,
        'CMS': 2,
        'UGS': 2,
        'GAS': 2,
    },
    'baseline': 0.03,
    'min_baseline': 0,
    'max_baseline': 0.06,
    'min_minor_di': 20,
    'min_total_di': 80,
    'extend_di_frc': 0.05,
    'flank': {
        'AOM': 8,
        'BFM': 6,
        'GWA': 6,
        'GNS': 6,
        'BFS': 6,
        'CMS': 6,
        'UGS': 6,
        'GAS': 6,
    },
}


rule build_h12:
    input:
        "agam-report-base/src/python/rockies.py",
        "scripts/build_h12.py",
        "ngs.sanger.ac.uk/production/ag1000g/phase1/selection.1.RC2/windowed/hstats.txt.gz",
    params:
        amplitude=h12_config['amplitude'],
        min_amplitude=h12_config['min_amplitude'],
        decay=h12_config['decay'],
        min_decay=h12_config['min_decay'],
        max_decay=lambda wildcards, output: h12_config['max_decay'][wildcards.population],
        baseline=h12_config['baseline'],
        min_baseline=h12_config['min_baseline'],
        max_baseline=h12_config['max_baseline'],
        min_minor_di=h12_config['min_minor_di'],
        min_total_di=h12_config['min_total_di'],
        extend_di_frc=h12_config['extend_di_frc'],
        flank=lambda wildcards, output: h12_config['flank'][wildcards.population],
    output:
        "docs/_static/data/signal/H12/{population}/{chromosome}",
        touch("docs/_static/data/signal/H12/{population}/{chromosome}/.make"),
    shell:
        "python scripts/build_h12.py "
        "    --population {wildcards.population}"
        "    --chromosome {wildcards.chromosome}"
        "    --amplitude {params.amplitude}"
        "    --min-amplitude {params.min_amplitude}"
        "    --decay {params.decay}"
        "    --min-decay {params.min_decay}"
        "    --max-decay {params.max_decay}"
        "    --baseline {params.baseline}"
        "    --min-baseline {params.min_baseline}"
        "    --max-baseline {params.max_baseline}"
        "    --min-minor-di {params.min_minor_di}"
        "    --min-total-di {params.min_total_di}"
        "    --extend-di-frc {params.extend_di_frc}"
        "    --flank {params.flank}"


rule tabulate_signals:
    input:
        "scripts/tabulate_signals.py",
#        "docs/_static/data/signal/H12/BFS/X/.make",
        expand("docs/_static/data/signal/H12/{pop}/{chrom}/.make", pop=pop_ids, chrom=chromosomes),
        # TODO more signals here
    output:
        "docs/_static/data/signals.csv",
    shell:
        "python scripts/tabulate_signals.py"


rule all:
    input:
        rules.tabulate_signals.output
